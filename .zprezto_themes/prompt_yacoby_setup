# Load dependencies.
pmodload 'helper'

function prompt_yacoby_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_yacoby_pwd="$MATCH"
    unset MATCH
  else
    _prompt_yacoby_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_yacoby_git_info {
  if (( _prompt_yacoby_precmd_async_pid > 0 )); then
    # Append Git status.
    if [[ -s "$_prompt_yacoby_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_yacoby_precmd_async_data"
      PROMPT='$(build_prompt)'
      unalias typeset
    fi

    # Reset PID.
    _prompt_yacoby_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function prompt_yacoby_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    typeset -p git_info >! "$_prompt_yacoby_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_yacoby_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_yacoby_pwd

  # Define prompts.
  PROMPT=''

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_yacoby_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_yacoby_precmd_async_pid" &>/dev/null
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi

  # Compute slow commands in the background.
  trap prompt_yacoby_git_info WINCH
  prompt_yacoby_precmd_async &!
  _prompt_yacoby_precmd_async_pid=$!
}

function build_prompt {
  prefix="$"
  if [[ "$(whoami)" == "root" ]]; then
    prefix="#"
  fi
  echo ''
  echo -n "$prefix "
  echo -n '%{%F{cyan}%}%n%{%F{white}%}@%{%F{green}%}%M %{%F{grey}%}'
  echo -n "$_prompt_yacoby_pwd"
  if (( $+functions[git-info] )); then
    if git-info; then
      echo -n " (${git_info:+${(e)git_info[status]}})"
    fi
  fi
  echo -n "$python_info[virtualenv]"
  echo ''
  echo "$prefix "
}

function prompt_yacoby_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_yacoby_precmd_async_pid=0
  _prompt_yacoby_precmd_async_data="${TMPPREFIX}-prompt_yacoby_data"

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_yacoby_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b' # completing

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  zstyle ':prezto:module:python:info:virtualenv' format ' (%v)'


  # Define prompts.
  PROMPT='$(build_prompt)'
  RPROMPT='[%D{%H:%M:%S}]'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_yacoby_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'yacoby'
}

prompt_yacoby_setup "$@"
